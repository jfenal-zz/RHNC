#!/usr/bin/perl
#
# $Id$
#

use strict;
use warnings;

use lib qw( . .. lib ../lib );
use RHNC;
use Pod::Usage;
use Carp;
use Getopt::Long qw(GetOptionsFromArray);
use Data::Dumper;

use vars qw( $rhnc );

=head1 NAME

rhnc-system - Operate on Satellite managed systems

=head1 USAGE

  rhnc-system command [options or parameters]

=head1 REQUIRED ARGUMENTS

At least command is required for this script to operate. No shell is
currently offered, although this may come in the future.

=head1 DESCRIPTION

The following commands are available:

=cut

=head2 destroy

Destroy a system profile

=cut

sub destroy {
    my @args = @_;
    my $verbose;

    my $result = GetOptionsFromArray( \@args, 'verbose|v+' => \$verbose, )
      or pod2usage(
        -exitval  => 254,
        -verbose  => 99,
        -sections => ['DESCRIPTION/destroy'],
      );

    carp "not implemented yet";
    foreach my $kname (@args) {
        my $sys = RHNC::System->get( $rhnc, $kname );
        if ( defined $sys ) {
            $sys->destroy( $rhnc, $kname );
        }
    }

    return 0;
}

=head2 get_details

Print detailed information about the given system

  rhnc-system gd [-v] profilename
  rhnc-system get [-v] profilename
  rhnc-system get_details [-v] profilename

=cut

sub get_details {
    my @args = @_;
    my $sys;
    my $verbose = 0;

    my $result = GetOptionsFromArray( \@args, 'verbose|v+' => \$verbose, )
      or pod2usage(
        -exitval  => 254,
        -verbose  => 99,
        -sections => ['DESCRIPTION/get_details'],
      );

    while ( $sys = shift @args ) {
        my $system = RHNC::System->get( $rhnc, $sys );
        $system->entitlements;
        if ( $verbose == 1 ) {
            $system->get_details;
            $system->registration_date;
        }
        elsif ( $verbose == 2 ) {
            $system->running_kernel;
            $system->base_channel;
            $system->child_channels;
            $system->memory;
            $system->network;
        }
        elsif ( $verbose == 3 ) {
            $system->custom_values;
            $system->cpu;
            $system->dmi;
            $system->devices;
            $system->network_devices;
        }

        print $system->as_string;

    }

    return 0;
}

=head2 list

List system groups

  rhnc-system l [-v]
  rhnc-system list [-v]

=cut

sub list {
    my @args = @_;
    my ($verbose);

    my $result = GetOptionsFromArray( \@args, 'verbose|v+' => \$verbose, )
      or pod2usage(
        -exitval  => 254,
        -verbose  => 99,
        -sections => ['DESCRIPTION/list'],
      );

    my $slist = RHNC::System->list($rhnc);

    my $nr = 0;
    foreach my $sys ( keys %{$slist} ) {
        printf( "%10d %-40s %s\n",
            $slist->{$sys}{id},
            $slist->{$sys}{name},
            $slist->{$sys}{last_checkin}->value );
    }
    print "\nTotal: " . scalar( keys %$slist ) . " systems\n"
      if $verbose;

    return 0;
}

=head2 rm_system

TODO

=cut

sub rm_system {
    my @args = @_;

    carp "not implemented yet";

    return 0;
}

=head2 channel

Channel manipulation.

  rhnc-system channel system          # list channel hierarchy
  rhnc-system channel -b system       # get base channel
  rhnc-system channel -c system       # get child channels
  rhnc-system channel -ab system      # get available base channels
  rhnc-system channel -ac system      # get available child channels

=cut

sub channel {
    my @args = @_;
    my $sys;
    my ( $verbose, $base, $child, $avail_base, $avail_child ) =
      ( 0, 0, 0, 0, 0 );

    my $result = GetOptionsFromArray(
        \@args,
        'verbose|v+'    => \$verbose,
        'base|b'        => \$base,
        'child|c'       => \$child,
        'availbase|ab'  => \$avail_base,
        'availchild|ac' => \$avail_child,
      )
      or pod2usage(
        -exitval  => 254,
        -verbose  => 99,
        -sections => ['DESCRIPTION/channel'],
      );
    $sys = shift @args;
    my $system = RHNC::System->get( $rhnc, $sys );

    if ($avail_base) {
        my @c = sort @{ $system->available_base_channels };
        if (@c) {
            print join( "\n", @c ) . "\n";
        }
        return 0;
    }

    if ($avail_child) {
        my @c = sort @{ $system->available_child_channels };
        if (@c) {
            print join( "\n", @c ) . "\n";
        }
        return 0;
    }

    if ( !$base && !$child ) {
        print $system->base_channel . "\n";
        my @cc = sort @{ $system->child_channels };
        if (@cc) {
            print "+ " . join( "\n+ ", @cc ) . "\n";
        }
        return 0;
    }

    if ($base) {
        print $system->base_channel . "\n";
        return 0;
    }
    if ($child) {
        my @cc = @{ $system->child_channels };
        if (@cc) {
            print join( "\n", @cc ) . "\n";
        }
    }
    return 0;
}

=head2 set

System properties manipulation.

  rhnc-system set -b channel system          # set base channel
  rhnc-system set -c channel,channel system  # set child channels

=cut

sub set {
    my @args = @_;
    my $sys;
    my ( $verbose, $base, $child, $avail_base, $avail_child ) =
      ( 0, 0, 0, 0, 0 );

    my $result = GetOptionsFromArray(
        \@args,
        'verbose|v+' => \$verbose,
        'base|b=s'   => \$base,
        'child|c=s'  => \$child,
      )
      or pod2usage(
        -exitval  => 254,
        -verbose  => 99,
        -sections => ['DESCRIPTION/channel'],
      );
    $sys = shift @args;
    my $system = RHNC::System->get( $rhnc, $sys );

    if ($base) {
        $system->base_channel($base);
        return 0;
    }
    if ($child) {
        my @cc = split( m{ , }imxs, $child );
        $system->child_channels( \@cc );
    }
    return 0;
}

=head1 OPTIONS

See various commands for options available.

=head1 DIAGNOSTICS

See L<EXIT STATUS>.

=head1 EXIT STATUS

TODO. 
This script will return 1 for success, 0 for failure. Not very
consistent for now, likely to change.

=head1 CONFIGURATION

See L<RHNC>.

=head1 DEPENDENCIES

=over 4

=item * L<RHNC>

=item * L<Pod::Usage>

=back

=head1 INCOMPATIBILITIES

See L<RHNC>.

=head1 BUGS AND LIMITATIONS

See L<RHNC>.

=head1 AUTHOR

Jerome Fenal L<< <jfenal@free.fr> >>.

=head1 LICENSE AND COPYRIGHT

Copyright 2009, 2010 Jerome Fenal, all rights reserved.

This module is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut

package main;

#
# Main
#

$rhnc = RHNC::Session->new();

my %func = (
    destroy              => \&destroy,
    gd                   => \&get_details,
    get                  => \&get_details,
    get_details          => \&get_details,
    l                    => \&list,
    li                   => \&list,
    list                 => \&list,
    ls                   => \&list_systems,
    list_systems         => \&list_systems,
    rp                   => \&rm_systems,
    rm_systems           => \&rm_systems,
    llp                  => \&list_latest_packages,
    list_latest_packages => \&list_latest_packages,
    channel              => \&channel,
    chan                 => \&channel,
    ch                   => \&channel,
);

my @args = @ARGV;
my $cmd  = shift @args;
my $rc;

if ( defined $cmd && defined $func{$cmd} ) {
    $rhnc = RHNC::Session->new();
    $rc   = $func{$cmd}->(@args);
    exit $rc;
}

pod2usage(
    -exitval => 254,
    -verbose => 99,
    -message => "No or unknown command given"
);

